#version 430 core

layout (local_size_x = 16, local_size_y = 16) in;

layout (r32f, binding = 0) readonly uniform image2D ElevationRead;
layout (r32f, binding = 1) writeonly uniform image2D ElevationWrite;
layout (rgba32f, binding = 2) readonly uniform image2D GradientRead;
layout (r8ui, binding = 6) readonly uniform uimage2D ConstraintMaskImage;

uniform ivec2 DebugPixel0;
uniform ivec2 DebugPixel1;

layout (std430, binding = 10) buffer DebugBuffer{ vec4 DebugData[]; };

const uint MASK_ELEVATION = 1u << 0;
const uint MASK_GRADIENT  = 1u << 1;
const uint MASK_NOISE     = 1u << 2;

bool IsValidFloat(float x) {
    return (x == x) && (abs(x) < 1e20);
}

void main(){

	ivec2 p = ivec2(gl_GlobalInvocationID.xy);

	if(p.x >= imageSize(ElevationRead).x || p.y >= imageSize(ElevationRead).y){
		return;
	}

	bool HasElevationMask = (imageLoad(ConstraintMaskImage, p).r & MASK_ELEVATION) != 0u;
	bool HasGradientMask  = (imageLoad(ConstraintMaskImage, p).r & MASK_GRADIENT)  != 0u;

	float Alpha = 0.0f;
	float Beta = 0.0f;

	if(HasElevationMask){
		Alpha = 0.0f;
		Beta = 0.0f;
	}
	else if(HasGradientMask){
		Beta = 0.5f;
		Alpha = 0.5f;
	}
	else{
		Alpha = 1.0f;
		Beta = 0.0f;
	}


	ivec2 Left = clamp(p + ivec2(-1, 0), ivec2(0), imageSize(ElevationRead) - ivec2(1));
	ivec2 Right = clamp(p + ivec2(1, 0), ivec2(0), imageSize(ElevationRead) - ivec2(1));
	ivec2 Down = clamp(p + ivec2(0, -1), ivec2(0), imageSize(ElevationRead) - ivec2(1));
	ivec2 Up = clamp(p + ivec2(0, 1), ivec2(0), imageSize(ElevationRead) - ivec2(1));

	float F_L = 0.25f * (imageLoad(ElevationRead, Left).r +
						imageLoad(ElevationRead, Right).r +
						imageLoad(ElevationRead, Down).r +
						imageLoad(ElevationRead, Up).r);

	float F_I = imageLoad(ElevationRead, p).r;

	vec3 Normal = imageLoad(GradientRead, p).rgb;
	ivec2 Neighbor = ivec2(0, 0);
	if (abs(Normal.x) > abs(Normal.y)){
		Neighbor = p + ivec2(-int(sign(Normal.x)), 0);
	}
	else {
		Neighbor = p + ivec2(0, -int(sign(Normal.y)));
	}
	Neighbor = clamp(Neighbor, ivec2(0), imageSize(ElevationRead) - ivec2(1));
	float F_N = imageLoad(ElevationRead, Neighbor).r;
	ivec2 I_d = p - Neighbor;
	vec2 Direction = vec2(I_d);
	vec2 Gradient = Normal.xy * abs(Normal.z);
	float Delta = dot(Gradient, Direction);

	float F_G = F_N + Delta;

	float NewHeight = Alpha * F_L + Beta * F_G + (1.0f - Alpha - Beta) * F_I;

	if(!IsValidFloat(NewHeight)){
		NewHeight = imageLoad(ElevationRead, p).r;
	}

	if(p == DebugPixel0){
		DebugData[0] = vec4(float(imageLoad(ConstraintMaskImage, p).r), 0.0f, 0.0f, 0.0f);
	}
	if(p == DebugPixel1){
		DebugData[1] = vec4(float(imageLoad(ConstraintMaskImage, p).r), 0.0f, 0.0f, 0.0f);
	}

	imageStore(ElevationWrite, p, vec4(NewHeight) );

}