#version 430 core

layout (local_size_x = 16, local_size_y = 16) in;

layout (r32f, binding = 0) readonly uniform image2D ElevationRead;
layout (r32f, binding = 1) writeonly uniform image2D ElevationWrite;
layout (rgba32f, binding = 2) readonly uniform image2D GradientRead;
layout (r8ui, binding = 6) readonly uniform uimage2D ConstraintMaskImage;

const uint MASK_ELEVATION = 1u << 0;
const uint MASK_GRADIENT  = 1u << 1;
const uint MASK_NOISE     = 1u << 2;

void main(){

	ivec2 p = ivec2(gl_GlobalInvocationID.xy);

	if(p.x >= imageSize(ElevationRead).x || p.y >= imageSize(ElevationRead).y){
		return;
	}

	bool HasElevationMask = (imageLoad(ConstraintMaskImage, p).r & MASK_ELEVATION) != 0u;

	if(HasElevationMask){
		imageStore(ElevationWrite, p, imageLoad(ElevationRead, p));	
		return;
	}

	ivec2 Left = p + ivec2(-1,  0);
	if(Left.x < 0) Left.x = 0;
	ivec2 Right= p + ivec2( 1,  0);
	if(Right.x >= imageSize(GradientRead).x) Right.x = imageSize(GradientRead).x - 1;
	ivec2 Down = p + ivec2( 0, -1);
	if(Down.y < 0) Down.y = 0;
	ivec2 Up   = p + ivec2( 0,  1);
	if(Up.y >= imageSize(GradientRead).y) Up.y = imageSize(GradientRead).y - 1;

	float LeftHeight = imageLoad(ElevationRead, Left).r;
	float RightHeight = imageLoad(ElevationRead, Right).r;
	float DownHeight = imageLoad(ElevationRead, Down).r;
	float UpHeight = imageLoad(ElevationRead, Up).r;

	float LaplacianHeight = 0.25 * (LeftHeight + RightHeight + DownHeight + UpHeight);

	vec3 Gradient = imageLoad(GradientRead, p).rgb;

	vec2 GuideGradient = Gradient.xy * Gradient.z;

	float GuidedHeight = 0.25 * (GuideGradient.x * (RightHeight - LeftHeight) + GuideGradient.y * (UpHeight - DownHeight));

	float NewHeight = LaplacianHeight + GuidedHeight;

	imageStore(ElevationWrite, p, vec4(NewHeight) );

}